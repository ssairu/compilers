% "Лабораторная работа 3.2 «Форматтер исходных текстов»"
% 4 июня 2025 г.
% Артём Пенкин, ИУ9-61Б

# Цель работы
Целью данной работы является приобретение навыков использования 
генератора синтаксических анализаторов bison.

# Индивидуальный вариант
Функциональный язык программирования с алгебраическими типами данных

Предложения языка могут заканчиваться на секцию where … wend, 
которая содержит определения вспомогательных функций.

# Реализация

## грамматика
```
<program> ::= <decl_list>

<decl_list> ::= <decl_list> <decl>
            | ε

<decl> ::= <type_decl>
         | <fun_decl>

<type_decl> ::= "type" ID ":" <constructor_list> "."

<constructor_list> ::= <id_list>
                    | <id_list> "|" <constructor_list>

<id_list> ::= ID
            | ID <id_list>

<fun_decl> ::= "fun" "(" ID <id_list> ")" "->" ID ":" <fun_body_list> "."

<fun_body_list> ::= <fun_body>
                  | <fun_body> "|" <fun_body_list>

<fun_body> ::= <pattern> "->" <expr_list>
             | <pattern> "->" <expr_list> "where" <local_defs> "wend"

<pattern> ::= <fun_arg>
            | "[" <pattern_list> "]"
            | "(" <pattern_list> ")"

<fun_arg> ::= ID
            | NUMBER
            | "_"

<pattern_list> ::= <pattern>
                 | <pattern> <pattern_list>

<expr> ::= ID
         | NUMBER
         | "[" <expr_list> "]"
         | "(" <expr_list> ")"

<expr_list> ::= <expr>
              | <expr> <expr_list>

<local_defs> ::= <fun_decl>
               | <fun_decl> <local_defs>

```

## lexer.l
```
%option reentrant noyywrap bison-bridge bison-locations
%option extra-type="struct Extra *"

%{
#include <stdio.h>
#include <stdlib.h>
#include "lexer.h"
#include "parser.tab.h"

#define YY_USER_ACTION \
  { \
    int i; \
    struct Extra *extra = yyextra; \
    if (!extra->continued) { \
      yylloc->first_line = extra->cur_line; \
      yylloc->first_column = extra->cur_column; \
    } \
    extra->continued = false; \
    for (i = 0; i < yyleng; ++i) { \
      if (yytext[i] == '\n') { \
        extra->cur_line += 1; \
        extra->cur_column = 1; \
      } else { \
        extra->cur_column += 1; \
      } \
    } \
    yylloc->last_line = extra->cur_line; \
    yylloc->last_column = extra->cur_column; \
  }

void yyerror(YYLTYPE *loc, yyscan_t scanner, const char *message) {
    printf("Error (%d,%d): %s\n", loc->first_line, loc->first_column, message);
}
%}

%%

"type"      { return TYPE; }
"fun"       { return FUN; }
"where"     { return WHERE; }
"wend"      { return WEND; }
":"         { return COLON; }
"|"         { return PIPE; }
"->"        { return ARROW; }
"["         { return LBRACK; }
"]"         { return RBRACK; }
"("         { return LPAREN; }
")"         { return RPAREN; }
"_"         { return UNDERSCORE; }
"."         { return DOT; }
[a-zA-Z_][a-zA-Z0-9_]*  { yylval->str = strdup(yytext); return ID; }
[0-9]*      { yylval->str = strdup(yytext); return NUMBER; }
[ \t\n]+    /* ignore */
"<<".*">>"  /* comments */

%%

void init_scanner(FILE *input, yyscan_t *scanner, struct Extra *extra) {
    extra->continued = false;
    extra->cur_line = 1;
    extra->cur_column = 1;
    yylex_init(scanner);
    yylex_init_extra(extra, scanner);
    yyset_in(input, *scanner);
}

void destroy_scanner(yyscan_t scanner) {
    yylex_destroy(scanner);
}

```

## lexer.h
```
#ifndef LEXER_H
#define LEXER_H

#include <stdbool.h>
#include <stdio.h>

#ifndef YY_TYPEDEF_YY_SCANNER_T
#define YY_TYPEDEF_YY_SCANNER_T
typedef void *yyscan_t;
#endif /* YY_TYPEDEF_YY_SCANNER_T */

struct Extra {
    bool continued;
    int cur_line;
    int cur_column;
};

void init_scanner(FILE *input, yyscan_t *scanner, struct Extra *extra);
void destroy_scanner(yyscan_t scanner);

#endif /* LEXER_H */

```

## parser.y
Обновлённый код без утечек памяти.
```
%{
#include <stdio.h>
#include <stdlib.h>
#include <string.h>
#include "lexer.h"

char* indent(char* str, int level) {
    if (!str) return strdup("");
    char indent_str[10];
    sprintf(indent_str, "%*s", level * 2, "");
    char* result = malloc(10000);
    result[0] = '\0';
    char* temp = strdup(str);
    char* line = strtok(temp, "\n");
    if (line != NULL) {
        strcat(result, indent_str);
        strcat(result, line);
        line = strtok(NULL, "\n");
        while (line != NULL) {
            strcat(result, "\n");
            strcat(result, indent_str);
            strcat(result, line);
            line = strtok(NULL, "\n");
        }
    }
    free(temp);
    return result;
}

void yyerror(yyscan_t scanner, const char *msg) {
    fprintf(stderr, "Error: %s\n", msg);
}
%}

%define api.pure
%locations
%lex-param {yyscan_t scanner}
%parse-param {yyscan_t scanner}

%union {
    char* str;
}

%token TYPE FUN WHERE WEND COLON PIPE ARROW LBRACK RBRACK LPAREN RPAREN UNDERSCORE DOT
%token <str> ID NUMBER

%type <str> program decl_list decl type_decl constructor_list id_list fun_arg fun_body_list
%type <str> fun_decl fun_body pattern pattern_list expr expr_list local_defs

%destructor { free($$); } <str>

%start program

%%

program:
    decl_list
    {
        printf("%s\n", $1);
        free($1);
    }
    ;

decl_list:
    /* empty */
    {
        $$ = strdup("");
    }
    | decl_list decl
    {
        $$ = malloc(10000);
        sprintf($$, "%s\n%s\n", $1, $2);
        free($1);
        free($2);
    }
    ;

decl:
    type_decl
    {
        $$ = $1;
    }
    | fun_decl
    {
        $$ = $1;
    }
    ;

type_decl:
    TYPE ID COLON constructor_list DOT
    {
        char* indented = indent($4, 1);
        $$ = malloc(10000);
        sprintf($$, "type %s:\n%s.", $2, indented);
        free(indented);
        free($2);
        free($4);
    }
    ;

constructor_list:
    id_list
    {
        $$ = $1;
    }
    | id_list PIPE constructor_list
    {
        $$ = malloc(10000);
        sprintf($$, "%s |\n%s", $1, $3);
        free($1);
        free($3);
    }
    ;

id_list:
    ID
    {
        $$ = $1;
    }
    | ID id_list
    {
        $$ = malloc(10000);
        sprintf($$, "%s %s", $1, $2);
        free($1);
        free($2);
    }
    ;

fun_decl:
    FUN LPAREN ID id_list RPAREN ARROW ID COLON fun_body_list DOT
    {
        char* indented = indent($9, 1);
        $$ = malloc(10000);
        sprintf($$, "fun (%s %s) -> %s:\n%s.", $3, $4, $7, indented);
        free(indented);
        free($3);
        free($4);
        free($7);
        free($9);
    }
    ;

fun_body_list:
    fun_body
    {
        $$ = $1;
    }
    | fun_body PIPE fun_body_list
    {
        $$ = malloc(10000);
        sprintf($$, "%s |\n%s", $1, $3);
        free($1);
        free($3);
    }
    ;

fun_body:
    pattern ARROW expr_list
    {
        $$ = malloc(10000);
        sprintf($$, "%s -> %s", $1, $3);
        free($1);
        free($3);
    }
    | pattern ARROW expr_list WHERE local_defs WEND
    {
        char* indented = indent($5, 1);
        $$ = malloc(10000);
        sprintf($$, "%s -> %s\nwhere\n%s\nwend", $1, $3, indented);
        free(indented);
        free($1);
        free($3);
        free($5);
    }
    ;

pattern:
    fun_arg
    {
        $$ = $1;
    }
    | LBRACK pattern_list RBRACK
    {
        $$ = malloc(10000);
        sprintf($$, "[%s]", $2);
        free($2);
    }
    | LPAREN pattern_list RPAREN
    {
        $$ = malloc(10000);
        sprintf($$, "(%s)", $2);
        free($2);
    }
    ;

fun_arg:
    ID
    {
        $$ = $1;
    }
    | NUMBER
    {
        $$ = $1;
    }
    | UNDERSCORE
    {
        $$ = strdup("_");
    }
    ;

pattern_list:
    pattern
    {
        $$ = $1;
    }
    | pattern pattern_list
    {
        $$ = malloc(10000);
        sprintf($$, "%s %s", $1, $2);
        free($1);
        free($2);
    }
    ;

expr:
    ID
    {
        $$ = $1;
    }
    | NUMBER
    {
        $$ = $1;
    }
    | LBRACK expr_list RBRACK
    {
        $$ = malloc(10000);
        sprintf($$, "[%s]", $2);
        free($2);
    }
    | LPAREN expr_list RPAREN
    {
        $$ = malloc(10000);
        sprintf($$, "(%s)", $2);
        free($2);
    }
    ;

expr_list:
    expr
    {
        $$ = $1;
    }
    | expr expr_list
    {
        $$ = malloc(10000);
        sprintf($$, "%s %s", $1, $2);
        free($1);
        free($2);
    }
    ;

local_defs:
    fun_decl
    {
        $$ = $1;
    }
    | fun_decl local_defs
    {
        $$ = malloc(10000);
        sprintf($$, "%s\n%s", $1, $2);
        free($1);
        free($2);
    }
    ;

%%

int main(int argc, char *argv[]) {
    FILE *input = stdin;
    yyscan_t scanner;
    struct Extra extra;

    if (argc > 1) {
        input = fopen(argv[1], "r");
        if (!input) {
            perror("fopen");
            return 1;
        }
    }

    init_scanner(input, &scanner, &extra);
    yyparse(scanner);
    destroy_scanner(scanner);

    if (input != stdin) {
        fclose(input);
    }

    return 0;
}

```

## run.sh
```
#!/bin/bash

flex lexer.l
bison -d -Wcounterexamples parser.y
gcc -o formatter lex.yy.c parser.tab.c -lfl
rm -f lex.yy.c parser.tab.c parser.tab.h
./formatter input.txt > output.txt

```

# Тестирование

Входные данные

```
<< Объявления типов >>
type List: Cons Int List | Nil.
type Pair: Pair Int Int.
type PairList: PCons Pair PairList | PNil.
type ListList: XCons List ListList | XNil.

<< Объединение двух списков >>
fun (zip List List) -> PairList:

(zip [Cons x xs] [Cons y ys]) -> [PCons [Pair x y] (zip xs ys)] |
(zip _ _) -> [PNil].

<< Декартово произведение >>
fun (cart_prod List List) -> PairList:

(cart_prod [Cons x xs] ys) -> (append (bind x ys) (cart_prod xs ys))
  where
    fun (bind Int List) -> PairList:

    (bind x [Cons y ys]) -> [PCons [Pair x y] (bind x ys)] |
    (bind _ [Nil]) -> [PNil].
  wend |
(cart_prod [Nil] _) -> [PNil].

<< Конкатенация списков пар >>
fun (append PairList PairList) -> PairList:

(append [PCons x xs] ys) -> [PCons x (append xs ys)] |
(append [PNil] ys) -> ys.

<< Расплющивание вложенного списка >>
fun (flat ListList) -> List:

(flat [XCons [Cons x xs] xss]) -> [Cons x (flat [XCons xs xss])] |
(flat [XCons [Nil] xss]) -> (flat xss) |
(flat [XNil]) -> [Nil].

<< Сумма элементов списка >>
fun (sum List) -> Int:

(sum [Cons x xs]) -> (add x (sum xs)) |
(sum [Nil]) -> 0.

<< Вычисление полинома по схеме Горнера >>
fun (polynom Int List) -> Int:

(polynom x [Nil]) -> 0 |
(polynom x [Cons coef coefs]) -> (add (mul (polynom x coefs) x) coef).

<< Вычисление полинома x^3 + x^2 + x + 1 >>
fun (polynom1111 Int) -> Int:

(polynom1111 x) -> (polynom x [Cons 1 [Cons 1 [Cons 1 [Cons 1 [Nil]]]]]).

```

Вывод на `stdout`

```
type List:
  Cons Int List |
  Nil.

type Pair:
  Pair Int Int.

type PairList:
  PCons Pair PairList |
  PNil.

type ListList:
  XCons List ListList |
  XNil.

fun (zip List List) -> PairList:
  (zip [Cons x xs] [Cons y ys]) -> [PCons [Pair x y] (zip xs ys)] |
  (zip _ _) -> [PNil].

fun (cart_prod List List) -> PairList:
  (cart_prod [Cons x xs] ys) -> (append (bind x ys) (cart_prod xs ys))
  where
    fun (bind Int List) -> PairList:
      (bind x [Cons y ys]) -> [PCons [Pair x y] (bind x ys)] |
      (bind _ [Nil]) -> [PNil].
  wend |
  (cart_prod [Nil] _) -> [PNil].

fun (append PairList PairList) -> PairList:
  (append [PCons x xs] ys) -> [PCons x (append xs ys)] |
  (append [PNil] ys) -> ys.

fun (flat ListList) -> List:
  (flat [XCons [Cons x xs] xss]) -> [Cons x (flat [XCons xs xss])] |
  (flat [XCons [Nil] xss]) -> (flat xss) |
  (flat [XNil]) -> [Nil].

fun (sum List) -> Int:
  (sum [Cons x xs]) -> (add x (sum xs)) |
  (sum [Nil]) -> 0.

fun (polynom Int List) -> Int:
  (polynom x [Nil]) -> 0 |
  (polynom x [Cons coef coefs]) -> (add (mul (polynom x coefs) x) coef).

fun (polynom1111 Int) -> Int:
  (polynom1111 x) -> (polynom x [Cons 1 [Cons 1 [Cons 1 [Cons 1 [Nil]]]]]).

```

# Вывод
В процессе выполнения данной работы я освоил генератор синтаксического анализа
bison, а также научился выполнять полезную нагрузку во время
выполнения синтаксического анализа и присваивания каждому узлу своего значения.